## Spock Primer
---
### Testes da Camada de Domínio
##### Groovy

Para facilitar a criação de testes, Spock recorre a uma linguagem de *scripting* Groovy. Esta é semelhante ao Java, e introduz algumas ferramentas orientadas ao desenvolvimento de testes.

##### Snippet 1
Localização: `backend/src/test/groovy/pt/ulisboa/tecnico/socialsoftware/humanaethica/activity/domain/CreateActivityMethodTest.groovy


```Java
@DataJpaTest
class CreateActivityMethodTest extends SpockTest {
    Institution institution = Mock()
    Theme theme = Mock()
    Activity otherActivity = Mock()
    def activityDto
	
    def setup() {
        given: "activity info"
        activityDto = new ActivityDto()
        activityDto.name = ACTIVITY_NAME_1
        activityDto.region = ACTIVITY_REGION_1
        activityDto.participantsNumberLimit = 2
        activityDto.description = ACTIVITY_DESCRIPTION_1
        activityDto.startingDate = DateHandler.toISOString(IN_TWO_DAYS)
        activityDto.endingDate = DateHandler.toISOString(IN_THREE_DAYS)
        activityDto.applicationDeadline = DateHandler.toISOString(IN_ONE_DAY)
    }
	
    def "create activity with theme and institution has another activity"() {
        given:
        otherActivity.getName() >> ACTIVITY_NAME_2
        institution.getActivities() >> [otherActivity]
        theme.getState() >> Theme.State.APPROVED
        def themes = [theme]
		
        when:
        def result = new Activity(activityDto, institution, themes)
		
        then: "check result"
        result.getInstitution() == institution
        result.getName() == ACTIVITY_NAME_1
        result.getRegion() == ACTIVITY_REGION_1
        result.getParticipantsNumberLimit() == 2
        result.getDescription() == ACTIVITY_DESCRIPTION_1
        result.getStartingDate() == IN_TWO_DAYS
        result.getEndingDate() == IN_THREE_DAYS
        result.getApplicationDeadline() == IN_ONE_DAY
        result.getThemes().size() == 1
        result.getThemes().get(0) == theme
        and: "invocations"
        1 * institution.addActivity(_)
        1 * theme.addActivity(_)
    }
    ...
```

Visto que certos testes apenas precisam de testar algumas variáveis ou métodos de uma classe, por vezes pode ser lento criar instâncias dessa classe, visto que ou existem muitos parâmetros ou existem várias dependências de outras classes. 

No exemplo acima, um teste relativo à criação de atividades onde uma instituição já possui outra atividade diferente da que vai ser criada. Estar a instanciar duas atividades com valores diferentes, uma instituição e um tema seria algo demoroso... então recorrem-se a *Mocks*.

`Mock()` permite-nos criar um modelo da classe em questão, onde as variáveis e os valores que os métodos devolvem são definidos no próprio teste! Claro que qualquer destes que não esteja instanciado potencialmente faz com que o teste falhe, mas é melhor à alternativa. ^1b9ce5

De seguida, chama-se a função `setup`, responsável por inicializar os métodos ou variáveis inseridos na mesma. De acordo com [a documentação](https://spockframework.org/spock/docs/1.3/spock_primer.html):

>`def setupSpec() {}    // runs once -  before the first feature method`
>`def setup() {}        // runs before every feature method`
>`def cleanup() {}      // runs after every feature method`
>`def cleanupSpec() {}  // runs once -  after the last feature method`

*Fixtures* são métodos que periodicamente são chamados antes ou depois de um ou todos os testes. Desta maneira não é preciso repetir a inicialização de objetos comuns a todos ou alguns testes.

Aqui chega-se ao essencial do SpockPrimer, *Feature Methods*. Estes métodos, cujo nome normalmente é um literal *String*, permite-nos ver a ação/reação dos elementos do sistema sem ter que correr o sistema inteiro. Por sua vez, esta é dividida em blocos, `given`, `when`, `then`, `expect`, `cleanup`, and `where`.

#### Detour à documentação do SpockPrimer

```Java
class Especificacao extends Specification {
	...
}
```

Uma classe que herde `Specification` contém métodos úteis para escrever especificações. Nos testes do projeto, estes herdam de `SpockTest` que é uma sub-classe de `Specification` com algumas utilizades como mensagens.

###### Bloco given

O primeiro bloco na função inicializa o cenário descrito pelo literal anterior. Este é opcional e pode ser omitido, onde nenhum outro bloco pode preceder este.

###### Blocos when and then

Estes ocorrem juntos e descrevem o par estímulo/resposta. O bloco `when` pode conter código arbitrário, enquanto que o bloco `then` é restrito a considções, condições de exceções, interações e definições de variáveis. Podem existir múltiplos pares destes blocos.

Para verificação de condições, basta usar condições boolean, não sendo preciso uma API de *assertion*. É recomendado manter entre 1 a 5 condições em um bloco `then`. Todas as expressões de topo nível são consideradas boolean.

Para verificação de exceções, basta inserir a chamada que levanta a exceção com `thrown()`. Isto faz com que caso não seja lançada uma exceção, é contado como erro.

Para além disso, pode-se aceder a membros de uma exceção nas linhas seguintes, para caso se queira verificar as características associadas ao mesmo.

No entanto é preciso ter cuidado. Por exemplo:

```Java
def "HashMap accepts null key"() {
  setup:
  def map = new HashMap()
  map.put(null, "elem")
}
```

```Java
def "HashMap accepts null key"() {
  given:
  def map = new HashMap()

  when:
  map.put(null, "elem")

  then:
  notThrown(NullPointerException)
}
```

Ambos estes pedaços de código testam se é possível inserir o valor `"elem"` com a chave de valor  `null` em um `HashMap`. No entanto, a segunda dá a entender o propósito do teste, enquanto que a primeira dá a parecer que o teste está incompleto.

###### Bloco expect

Semelhante ao bloco then, mas mais restrito no sentido que apenas pode conter condições e definições de variáveis. Mais utilizado em situações de métodos puramente funcionais, enquanto que `when` e `then` servem para métodos com efeitos secundários como exceções.

###### Bloco cleanup

Ocorre depois do bloco `when` e não pode ser repetido. Tal como um método cleanup, server para libertar recursos utilizados pela função de teste como um ficheiro.

###### Bloco where

Este é sempre o último bloco e não pode ser repetido. É orientado a métodos que tenham muita informação, de modo a intercalar os diferentes valores.

```Java
def "computing the maximum of two numbers"() {
  expect:
  Math.max(a, b) == c

  where:
  a << [5, 3]
  b << [1, 9]
  c << [5, 9]
}
```

Neste caso, há duas versões do método, uma em que `a`, `b` e `c` são 5, 1 e 5, respetivamente, enquanto que na segunda versão são 3, 9 e 9.


Voltando ao exemplo, para criar uma atividade é preciso um objeto da camada de modelo de domínio (ver o que é um [[P02#Domain Model Layer|DTO]]). 

Assim, incializam-se todas as variáveis necessárias para criar o objeto `Activity`. Depois, começa-se o teste no bloco `when`, onde se invoca o construtor `Activity`, em que caso este levante uma exceção, o teste falha. Não falhando, passa-se ao bloco `then`, que verifica as variáveis, o que faz sentido uma vez que o construtor só funciona corretamente se a instância for inicializada da forma pretendida.

Adicionalmente, verifica-se as chamadas com o bloco `and`, e caso não haja estritamente 1 chamada com quaisquer parâmetros das funções `addActivity()` da classe Institution ou `addActivity` da classe Theme, o teste falha.

#### Teste 2

Este teste é a continuação da classe anterior, no entanto passamos a testar por falhas ao passar valores que não são permitidos enquanto constrangimentos das variáveis em vez de sucesso.

```Java
    @Unroll
    def "create activity and violate invariants name, region, and description are required: name=#name | region=#region | description=#description"() {
        given:
        otherActivity.getName() >> ACTIVITY_NAME_2
        institution.getActivities() >> [otherActivity]
        theme.getState() >> Theme.State.APPROVED
        def themes = [theme]
        and: "an activity dto"
        activityDto = new ActivityDto()
        activityDto.setName(name)
        activityDto.setRegion(region)
        activityDto.setParticipantsNumberLimit(1)
        activityDto.setDescription(description)
        activityDto.setApplicationDeadline(DateHandler.toISOString(IN_ONE_DAY))
        activityDto.setStartingDate(DateHandler.toISOString(IN_TWO_DAYS))
        activityDto.setEndingDate(DateHandler.toISOString(IN_THREE_DAYS))
        when:
        new Activity(activityDto, institution, themes)
        then:
        def error = thrown(HEException)
        error.getErrorMessage() == errorMessage
        where:
        name            | region            | description            || errorMessage
        null            | ACTIVITY_REGION_1 | ACTIVITY_DESCRIPTION_1 || ErrorMessage.ACTIVITY_NAME_INVALID
        " "             | ACTIVITY_REGION_1 | ACTIVITY_DESCRIPTION_1 || ErrorMessage.ACTIVITY_NAME_INVALID
        ACTIVITY_NAME_1 | null              | ACTIVITY_DESCRIPTION_1 || ErrorMessage.ACTIVITY_REGION_NAME_INVALID
        ACTIVITY_NAME_1 | " "               | ACTIVITY_DESCRIPTION_1 || ErrorMessage.ACTIVITY_REGION_NAME_INVALID
        ACTIVITY_NAME_1 | ACTIVITY_REGION_1 | null                   || ErrorMessage.ACTIVITY_DESCRIPTION_INVALID
        ACTIVITY_NAME_1 | ACTIVITY_REGION_1 | "  "                   || ErrorMessage.ACTIVITY_DESCRIPTION_INVALID
    }
    ...
```

Neste teste a inicialização de variáveis e bloco de teste ocorrem da mesma maneira. No entanto, de modo a testar as várias combinações de variáveis inválidas, é usado um truque em que em vez de escrever mútiplos testes individuais, basta criar uma tabela com as combinações de valores que as variáveis `name`,  `region` e  `description` podem tomar.

Como se pode observar, estas variáveis não têm nenhum valor atribuido no bloco `given`. Desta maneira, são usadas nesse mesmo bloco para inicializar parâmetros do `activityDto`, mas que só tomam valor posteriormente. 

Isto é possível visto que em tempo de compilação, o Groovy interpreta esta tabela e para cada linha de combinação de variáveis cria um teste independente.

Para efeitos de verificar se as exceções correspondem a certos parâmetros errados, apenas se varia uma das variáveis de cada vez, sendo que convém manter as outras corretas para isolar o erro. No entanto, pode-se atribuir qualquer valor **estático** nesta tabela.


