#### Testes de Web Service

A semântica dos testes mantém-se semelhante aos da camada de domínio e da camada de serviços. No entanto aqui já é necessário emular um servidor.

Localização: `backend/src/test/groovy/pt/ulisboa/tecnico/socialsoftware/humanaethica/activity/webservice/GetActivitiesWebServiceIT.groovy`

```Java
@SpringBootTest(webEnvironment = SpringBootTest.WebEnvironment.RANDOM_PORT)
class RegisterActivityWebServiceIT extends SpockTest {
    @LocalServerPort
    private int port
    def activityDto
    
    def setup() {
        deleteAll()
        webClient = WebClient.create("http://localhost:" + port)
        headers = new HttpHeaders()
        headers.setContentType(MediaType.APPLICATION_JSON)
        def theme = createTheme(THEME_NAME_1, Theme.State.APPROVED,null)
        def themesDto = new ArrayList<ThemeDto>()
        themesDto.add(new ThemeDto(theme,false, false, false))
        activityDto = createActivityDto(ACTIVITY_NAME_1,ACTIVITY_REGION_1,2,ACTIVITY_DESCRIPTION_1,
                IN_ONE_DAY,IN_TWO_DAYS,IN_THREE_DAYS,themesDto)
    }
	    
    def "login as member, and create an activity"() {
        given:
        demoMemberLogin()
        
        when:
        def response = webClient.post()
                .uri('/activities')
                .headers(httpHeaders -> httpHeaders.putAll(headers))
                .bodyValue(activityDto)
                .retrieve()
                .bodyToMono(ActivityDto.class)
                .block()
                
        then: "check response data"
        response.name == ACTIVITY_NAME_1
        response.region == ACTIVITY_REGION_1
        response.participantsNumberLimit == 2
        response.description == ACTIVITY_DESCRIPTION_1
        response.startingDate == DateHandler.toISOString(IN_TWO_DAYS)
        response.endingDate == DateHandler.toISOString(IN_THREE_DAYS)
        response.applicationDeadline == DateHandler.toISOString(IN_ONE_DAY)
        response.themes.get(0).getName() == THEME_NAME_1
        and: 'check database data'
        activityRepository.count() == 1
        def activity = activityRepository.findAll().get(0)
        activity.getName() == ACTIVITY_NAME_1
        activity.getRegion() == ACTIVITY_REGION_1
        activity.getParticipantsNumberLimit() == 2
        activity.getDescription() == ACTIVITY_DESCRIPTION_1
        activity.getStartingDate().withNano(0) == IN_TWO_DAYS.withNano(0)
        activity.getEndingDate().withNano(0) == IN_THREE_DAYS.withNano(0)
        activity.getApplicationDeadline().withNano(0) == IN_ONE_DAY.withNano(0)
        activity.themes.get(0).getName() == THEME_NAME_1
        cleanup:
        deleteAll()
    }
    ...
}
```

A primeira anotação apenas serve como um "Mock" ao servidor, de modo a emular pedidos HTTP. Isto gera uma porta aleatória, que é dada na variável da anotação `@LocalServerPort`. 

No `setup` é necessário primerio eliminar toda a informação da base de dados, uma vez que nestes testes já se escreve para a mesma. Caso contrário os testes não seriam consistentes para cada execução. De seguida prepara-se o cliente web com o URL e a porta gerada anteriormente, cabelhaços e a informação destes preenchida.

Por fim criam-se instâncias de objetos agregados ao teste, neste caso como é um teste relativo a atividades insere-se um tema e cria-se um `activityDto` para o pedido.

Aqui já é necessário ter em consideração o estado de quem faz o pedido. Para tal, no [[P03#Detour à documentação do SpockPrimer|bloco]] `given` autentica-se como *demo* através da função `demoMemberLogin()`. A autenticação de um utilizador é feito através de *tokens*, que são incluidos no header quando se chama a função anterior.

No bloco `when` compõe-se o request através dos métodos da *framework*. primeiro define-se o pedido como *POST*, adiciona-se o URI `/activities`, definem-se os headers com os anteriormente criados, insere-se no valor do corpo. A função `retrieve()` vai buscar o corpo da resposta, `bodyToMono()` converte esta informação de *Json* para um objeto do tipo `ActivityDto`.

Para o teste de erros, também é semelhante aos testes das camadas anteriores, onde se varia um campo de cada vez e verifica-se que o erro correspondente é levantado.

### Swagger

Swagger é uma ferramenta de documentação gerada a partir do código que pode ser executada. Para usar tal, é necessário iniciar o serviço de backend com `mvn clean spring-boot:run` que por predefinição corre na porta 8080.

Para abrir o UI da ferramenta, basta ir ao URL `localhost:8080/swagger-ui.html`, que nos redireciona para um exemplo da interface Swagger. Então é ainda necessário colocar na barra de pesquisa `/api-docs`. Assim devem aparecer todos os controladores disponíveis.

Por exemplo, ao testar um pedido POST no URI `/activities`, a resposta é da série 400, porque o utilizador a fazer o pedido não está autenticado. Para aceder à autenticação que os métodos como `demoMemberLogin()` fazem, basta procurar pelo `auth-controller`, onde o URI `auth/demo/member` faz exatamente essa função. Depois de obter o token, basta inseri-lo no topo da página onde diz *Authorize*, e colar o token.

No entanto, ainda é necessário uma *payload* com a informação do `activityDto` para registar uma nova atividade: (Alterar as datas para valores válidos!)

```Json
{
	"id":null,
	"name": "A2",
	"region": "Lisbon",
	"participantsNumber": 3,
	"themes": [],
	"creationDate": "-",
	"startingDate": "2024-02-04T18:43:00+00:00",
	"endingDate": "2024-02-54T18:43:00+00:00",
	"applicationDeadline": "2024-02-03T18:43:00+00:00"
}
```

Agora cola-se a informação no corpo do request, e é devolvido um código 200 com a lista de atividades. Para verificar se funcionou, pode testar o request do tipo *GET* do URI `/activities`.