### Service Layer

A Service Layer é uma camada transacional em forma de API que visa criar uma barreira entre os objetos persistentes relativamente à camada a respeito da JPA e as restantes camadas acima da mesma.

Localização: `backend/src/main/java/pt/ulisboa/tecnico/socialsoftware/humanaethica/activity/ActivityService.java`

```Java
@Service
public class ActivityService {
    @Autowired
    ActivityRepository activityRepository;
    @Autowired
    ThemeRepository themeRepository;
    @Autowired
    UserRepository userRepository;
    @Autowired
    InstitutionRepository institutionRepository;
	
	...
	
	@Transactional(isolation = Isolation.READ_COMMITTED)
    public ActivityDto registerActivity(Integer userId, ActivityDto activityDto) {
        if (userId == null) throw new HEException(USER_NOT_FOUND);
        Member member = (Member) userRepository.findById(userId).orElseThrow(() -> new HEException(USER_NOT_FOUND, userId));
        Institution institution = member.getInstitution();
        List<Theme> themes = getThemes(activityDto);
        Activity activity = new Activity(activityDto, institution, themes);
        activityRepository.save(activity);
        return new ActivityDto(activity, true);
    }
```

A anotação `@Service` serve para disponibilizar uma classe como um serviço da framework Spring, mais especificamente da ferramenta Bean. `@Autowired` é relativo à criação de instâncias das variáveis, onde se segue o padrão de criação *Single Factory*.

Como se pode observar, os métodos do serviço não recebem objetos persistentes, só informação. Isto acontece porque saímos da camada de dados, e isto é propositado ao nível da *framework*. Assim, ao criar uma barreira entre a manipulação aos dados, facilita-se a comunicação entre o controlador e a camada de persistência.

Na linha seguinte:

`Member member = (Member) userRepository.findById(userId).orElseThrow(() -> new HEException(USER_NOT_FOUND, userId));` 

É feito um acesso ao método `findById` do `userRepository`. No entanto, na classe desse repositório (`backend/src/main/java/pt/ulisboa/tecnico/socialsoftware/humanaethica/user/UserService.Java`), a classe encontra-se vazia! Isto é porque `findById` é uma função built-in da *framework*, e de acordo com [esta tabela](https://www.tutorialspoint.com/spring_boot_jpa/spring_boot_jpa_repository_methods.htm):

| Method                                                         | Description                                                         |
| -------------------------------------------------------------- | ------------------------------------------------------------------- |
| **count(): long**<br>                                          | returns the number of entities available.                           |
| **delete(Employee entity): void**                              | deletes an entity.                                                  |
| **deleteAll():void**                                           | deletes all the entities.                                           |
| **deleteAll(Iterable< extends Employee > entities):void**      | deletes the entities passed as argument.                            |
| **deleteAll(Iterable< extends Integer > ids):void**            | deletes the entities identified using their ids passed as argument. |
| **existsById(Integer id):boolean**                             | checks if an entity exists using its id.                            |
| **findAll():Iterable< Employee >**                             | returns all the entities.                                           |
| **findAllByIds(Iterable< Integer > ids):Iterable< Employee >** | returns all the entities identified using ids passed as argument.   |
| **findById(Integer id):Optional< Employee >**                  | returns an entity identified using id.                              |
| **save(Employee entity): Employee**                            | saves an entity and return the updated one.                         |
| **saveAll(Iterable< Employee> entities): Iterable< Employee>** | saves all entities passed and return the updated entities.          |
Desta maneira, a linha anterior procura o utlizador pelo `userId` especificado. Caso o não encontre levanta uma exceção, senão continua. 

Quando se obtém um membro, a instituição associada ao mesmo é também obtida, enquanto que os temas não são armazenados no objeto `Member`. Isso obriga-nos a aceder ao [[P02#DTO|DTO]] da classe `Themes` e procurar por todos os temas associados à atividade. Assim, ao obter os três elementos requiridos para construir um objeto `Activity`, pode-se formar uma atividade de modo a poder guardar uma nova instância do mesmo através de `activityRepository.save(activity)`. Como este método retorna um objeto `ActivityDto`, constrói-se um `ActivityDto` com o objeto `activity` anterior.

### Service Layer Tests

#### Teste 1


Localização: `backend/src/test/groovy/pt/ulisboa/tecnico/socialsoftware/humanaethica/activity/service/RegisterActivityServiceTest.groovy`

```Java
@DataJpaTest
class RegisterActivityServiceTest extends SpockTest {
    public static final String EXIST = "exist"
    public static final String NO_EXIST = "noExist"
    def institution
    def member
    def theme
	    
    def setup() {
        member = authUserService.loginDemoMemberAuth().getUser()
        institution = institutionService.getDemoInstitution()
        theme = new Theme(THEME_NAME_1, Theme.State.APPROVED,null)
        themeRepository.save(theme)
    }
	    
    def "register activity"() {
        given: "an activity dto"
        def themesDto = new ArrayList<>()
        themesDto.add(new ThemeDto(theme,false,false,false))
        def activityDto = createActivityDto(ACTIVITY_NAME_1,ACTIVITY_REGION_1,1,ACTIVITY_DESCRIPTION_1,
                IN_ONE_DAY,IN_TWO_DAYS,IN_THREE_DAYS,themesDto)
        when:
        def result = activityService.registerActivity(member.getId(), activityDto)
        then: "the returned data is correct"
        result.name == ACTIVITY_NAME_1
        result.region == ACTIVITY_REGION_1
        result.participantsNumberLimit == 1
        result.description == ACTIVITY_DESCRIPTION_1
        result.startingDate == DateHandler.toISOString(IN_TWO_DAYS)
        result.endingDate == DateHandler.toISOString(IN_THREE_DAYS)
        result.applicationDeadline == DateHandler.toISOString(IN_ONE_DAY)
        result.institution.id == institution.id
        result.getThemes().get(0).getName() == THEME_NAME_1
        result.getState() == Activity.State.APPROVED.name()
        and: "the activity is saved in the database"
        activityRepository.findAll().size() == 1
        and: "the stored data is correct"
        def storedActivity = activityRepository.findById(result.id).get()
        storedActivity.name == ACTIVITY_NAME_1
        storedActivity.region == ACTIVITY_REGION_1
        storedActivity.participantsNumberLimit == 1
        storedActivity.description == ACTIVITY_DESCRIPTION_1
        storedActivity.startingDate == IN_TWO_DAYS
        storedActivity.endingDate == IN_THREE_DAYS
        storedActivity.applicationDeadline == IN_ONE_DAY
        storedActivity.institution.id == institution.id
    }
	    
    ...
    @TestConfiguration
    static class LocalBeanConfiguration extends BeanConfiguration {}
}
```

Para os testes da Camada de Serviço, é utilizada a mesma classe que os [[P03#Spock Primer|testes da Camada de Domínio]], onde também se utiliza a mesma classe de utilizade `SpockTest`.

Nesta camada já não se utilizam [[P03#^1b9ce5|Mocks]]. Então para testar diferentes objetos é necessário criar instâncias dos mesmos.

Neste caso, precisamos de ter cuidado a certas instâncias de classes. Por exemplo, para referenciar serviços, não convém criar novas instâncias ou instâncias locais. Para tal, nas duas últimas linhas

```Java
    @TestConfiguration
    static class LocalBeanConfiguration extends BeanConfiguration {}
```

Deste modo, a classe de configuração tem funções com a anotação `@Bean` que criam a instância singular de um serviço.

O teste torna-se um pouco mais complexo no que toca à inicialização de variáveis. Para tal, os serviços proporcionam *demos*, exemplos das classes a testar e no caso dos temas é necessário criar um novo `Theme`, bem como registá-lo no repositório visto que não existe um *demo* associado a temas.

Já no primeiro teste, que tem o objetivo de registar uma nova atividade, para tal cria-se um `ThemeDto`, seguido de invocar a função `createActivityDto` definida na classe `SpockTest`.  De seguida, regista-se a nova instância da classe no serviço `activityService`, e depois verifica-se não só os valores do registo, mas também o número de instâncias que existem, e por fim também se faz uma "*query*" em `def storedActivity = activityRepository.findById(result.id).get()`.

#### Teste 2

Este segundo teste é semelhante ao [[P03#Teste 2| segundo teste da Camada de Domínio]] na sua estrutura:

```Java
....
	@Unroll
    def 'invalid arguments: name=#name | memberId=#memberId | themeId=#themeId'() {
        given: "an activity dto"
        def themesDto = new ArrayList<>()
        themesDto.add(getThemeDto(themeId))
        def activityDto = createActivityDto(name,ACTIVITY_REGION_1,1,ACTIVITY_DESCRIPTION_1,
                IN_ONE_DAY,IN_TWO_DAYS,IN_THREE_DAYS,themesDto)
        when:
        activityService.registerActivity(getMemberId(memberId), activityDto)
        then:
        def error = thrown(HEException)
        error.getErrorMessage() == errorMessage
        and: "no activity is stored in the database"
        activityRepository.findAll().size() == 0
        where:
        name            | memberId | themeId  || errorMessage
        null            | EXIST    | EXIST    || ErrorMessage.ACTIVITY_NAME_INVALID
        ACTIVITY_NAME_1 | null     | EXIST    || ErrorMessage.USER_NOT_FOUND
        ACTIVITY_NAME_1 | NO_EXIST | EXIST    || ErrorMessage.USER_NOT_FOUND
        ACTIVITY_NAME_1 | EXIST    | null     || ErrorMessage.THEME_NOT_FOUND
        ACTIVITY_NAME_1 | EXIST    | NO_EXIST || ErrorMessage.THEME_NOT_FOUND
	}
	    
    def getMemberId(memberId){
        if (memberId == EXIST)
            return member.id
        else if (memberId == NO_EXIST)
            return 222
        return null
    }
		
	def getThemeDto(themeId) {
        if (themeId == EXIST)
            return new ThemeDto(theme,false,false,false)
        else if (themeId == NO_EXIST) {
            def themeDto = new ThemeDto()
            themeDto.id = 222
            return themeDto
        }
        else
            return new ThemeDto()
    }
```

Onde a única diferença é que se incluem funções auxiliares para tratar dos diferentes parâmetros das instâncias associadas. Isto porque ao variar diferentes variáveis no bloco `given` onde tais valores não são válidos, isso simplesmente levantaria exceções ao criar as instâncias, e não se pretende testar tal, mas sim através do `registerActivity`.

